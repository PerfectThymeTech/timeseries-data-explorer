// Create raw tables
.create-merge tables
    ott_raw(
        Country: string,
        Service: string,
        ServiceType: string,
        Device: string,
        Title: string,
        Isp: string,
        CdnHostname: string,
        CdnPop: string,
        AppReleaseVersion: string,
        Browser: string,
        Os: string,
        Player: string,
        HappinessScore: real,
        UserId: string,
        EndTime: datetime,
        Hash: string
    ),
    iptv_raw(
        Country: string,
        Service: string,
        ServiceType: string,
        Topology2: string,
        Topology3: string,
        Topology4: string,
        Topology5: string,
        Title: string,
        StbModel: string,
        StbManufacturer: string,
        StbSwVersion: string,
        StreamingProtocol: string,
        CdnHostname: string,
        CdnPop: string,
        ChannelName: string,
        HappinessScore: real,
        UserId: string,
        EndTime: datetime,
        Hash: string
    )

// Create curated tables
.create-merge tables
    ott_happiness(
        Country: string,
        Service: string,
        ServiceType: string,
        Device: string,
        Title: string,
        Isp: string,
        CdnHostname: string,
        CdnPop: string,
        AppReleaseVersion: string,
        Browser: string,
        Os: string,
        Player: string,
        AvgHappinessScore: real,
        NumberOfUsers: long,
        Timestamp: datetime
    ),
    iptv_happiness(
        Country: string,
        Service: string,
        ServiceType: string,
        Topology2: string,
        Topology3: string,
        Topology4: string,
        Topology5: string,
        Title: string,
        StbModel: string,
        StbManufacturer: string,
        StbSwVersion: string,
        StreamingProtocol: string,
        CdnHostname: string,
        CdnPop: string,
        ChannelName: string,
        AvgHappinessScore: real,
        NumberOfUsers: long,
        Timestamp: datetime
    )

// Create helper tables
.create-merge tables
    thresholds (
        name: string,
        value: real,
        timestamp: datetime
    )

// Create function
.create-or-alter function with (docstring = 'Function to get latest threshold value for named threshold', folder='thresholds') GetThreshold(valueName: string) {
    toscalar(
        thresholds
        | where name == valueName
        | order by timestamp desc
        | limit 1
        | summarize max(value)
    )
}

.create-or-alter function with (docstring = 'Function to get average happiness scores for iptv data', folder='iptv') IptvGetAvgHappinessScore() {
    let currentTimestamp = now();
    let movingWindowInMinutes = GetThreshold('movingWindowInMinutes');
    iptv_raw
    | summarize
        AvgHappinessScore = avg(HappinessScore),
        NumberOfUsers = count_distinct(UserId),
        Timestamp = currentTimestamp
        by
        Country,
        Service,
        ServiceType,
        Topology2,
        Topology3,
        Topology4,
        Topology5,
        Title,
        StbModel,
        StbManufacturer,
        StbSwVersion,
        StreamingProtocol,
        CdnNodeHostname,
        CdnPop,
        ChannelName
}

.create-or-alter function with (docstring = 'Function to get average happiness scores for ott data', folder='ott') OttGetAvgHappinessScore() {
    let currentTimestamp = now();
    let movingWindowInMinutes = GetThreshold('movingWindowInMinutes');
    ott_raw
    | summarize
        AvgHappinessScore = avg(HappinessScore),
        NumberOfUsers = count_distinct(UserId),
        Timestamp = currentTimestamp
        by
        Country,
        Service,
        ServiceType,
        Device,
        Title,
        Isp,
        CdnNodeHostname,
        CdnPop,
        AppReleaseVersion,
        Browser,
        Os,
        Player
}

.create-or-alter function with (docstring = 'Function to get average happiness scores for ott data for one column', folder='ott') OttGetAvgHappinessScoreForOneColumn(columnName: string) {
    ott_happiness
    | extend TotalHappiness = NumberOfUsers * AvgHappinessScore
    | summarize
        SumTotalHappiness = sum(TotalHappiness),
        SumNumberOfUsers = sum(NumberOfUsers)
        by
        Timestamp,
        column_ifexists(columnName, 'columnName does not exist')
    | extend AvgHappinessScore = SumTotalHappiness / SumNumberOfUsers
    | project-away SumTotalHappiness
}

.create-or-alter function with (docstring = 'Function to get average happiness scores for ott data for two columns', folder='ott') OttGetAvgHappinessScoreForTwoColumns(columnName1: string, columnName2: string) {
    ott_happiness
    | extend TotalHappiness = NumberOfUsers * AvgHappinessScore
    | summarize
        SumTotalHappiness = sum(TotalHappiness),
        SumNumberOfUsers = sum(NumberOfUsers)
        by
        Timestamp,
        column_ifexists(columnName1, 'columnName1 does not exist'),
        column_ifexists(columnName2, 'columnName2 does not exist')
    | extend AvgHappinessScore = SumTotalHappiness / SumNumberOfUsers
    | project-away SumTotalHappiness
}

.create-or-alter function with (docstring = 'Function to get average happiness scores for iptv data for one column', folder='iptv') IptvGetAvgHappinessScoreForOneColumn(columnName: string) {
    iptv_happiness
    | extend TotalHappiness = NumberOfUsers * AvgHappinessScore
    | summarize
        SumTotalHappiness = sum(TotalHappiness),
        SumNumberOfUsers = sum(NumberOfUsers)
        by
        Timestamp,
        column_ifexists(columnName, 'columnName does not exist')
    | extend AvgHappinessScore = SumTotalHappiness / SumNumberOfUsers
    | project-away SumTotalHappiness
}

.create-or-alter function with (docstring = 'Function to get average happiness scores for iptv data for two columns', folder='iptv') IptvGetAvgHappinessScoreForTwoColumns(columnName1: string, columnName2: string) {
    iptv_happiness
    | extend TotalHappiness = NumberOfUsers * AvgHappinessScore
    | summarize
        SumTotalHappiness = sum(TotalHappiness),
        SumNumberOfUsers = sum(NumberOfUsers)
        by
        Timestamp,
        column_ifexists(columnName1, 'columnName1 does not exist'),
        column_ifexists(columnName2, 'columnName2 does not exist')
    | extend AvgHappinessScore = SumTotalHappiness / SumNumberOfUsers
    | project-away SumTotalHappiness
}

////////////////////////////////////////////////
// Create materialized view and view for testing
////////////////////////////////////////////////

// Create materalized view
.create ifnotexists materialized-view with (docString='IPTV Happiness Score Average', folder='iptv') iptv_happiness_mv on table iptv_raw {
    iptv_raw
    | summarize
        AvgHappinessScore = avg(HappinessScore),
        NumberOfUsers = dcount(UserId)
        by
        Country,
        Service,
        ServiceType,
        Topology2,
        Topology3,
        Topology4,
        Topology5,
        Title,
        StbModel,
        StbManufacturer,
        StbSwVersion,
        StreamingProtocol,
        CdnHostname,
        CdnPop,
        ChannelName
}

.create ifnotexists materialized-view with (docString='OTT Happiness Score Average', folder='ott') ott_happiness_mv on table ott_raw {
    ott_raw
    | summarize
        AvgHappinessScore = avg(HappinessScore),
        NumberOfUsers = dcount(UserId)
        by
        Country,
        Service,
        ServiceType,
        Device,
        Title,
        Isp,
        CdnHostname,
        CdnPop,
        AppReleaseVersion,
        Browser,
        Os,
        Player
}

// Create views
.create-or-alter function with (view=true, docstring='View to capture critical iptv items below the happiness threshold', folder='iptv') iptv_happiness_critical_mv {
    iptv_happiness_mv
    | where AvgHappinessScore < GetThreshold('happiness') and NumberOfUsers > GetThreshold('minimumNumberOfUsersPerWindow')
}

.create-or-alter function with (view=true, docstring='View to capture critical ott items below the happiness threshold', folder='ott') ott_happiness_critical_mv {
    ott_happiness_mv
    | where AvgHappinessScore < GetThreshold('happiness') and NumberOfUsers > GetThreshold('minimumNumberOfUsersPerWindow')
}
